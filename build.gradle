// 构建脚本，最先运行，必须放在 plugins 前面
buildscript {
    print("hello iplus")

    ext {
        springBootVersion = '2.3.2.RELEASE'
    }

    repositories {
        maven {
            url 'https://maven.aliyun.com/repository/public'
        }
        jcenter()
    }

    dependencies {
        // springboot 插件；依赖管理，构建配置（比如启动类）等
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

ext {

    rxjava = [
        version: '3.0.5'
    ]

    jsr305 = [
        version: '3.0.2'
    ]

    junit = [
        version: '5.6.2'
    ]
}

// 所有项目共享配置
allprojects {
    apply plugin: "java"
    // springboot 依赖管理插件，可以不指定版本号
    apply plugin: "io.spring.dependency-management"

    // 字符编码
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    group = 'xyz.liujin'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '11'

    sourceSets {
        main {
            resources {
                //可以将java目录下的所有非.java资源打包到classes下
                srcDir "src/main/java"
            }
        }
    }

    // 导入 mavenBom 管理依赖，后面可以不用指定版本号
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
        }
    }

    repositories {
        maven {
            url 'https://maven.aliyun.com/repository/public'
        }
        maven {
            url 'https://maven.aliyun.com/repository/central'
        }
        maven {
            url 'https://maven.aliyun.com/repository/gradle-plugin'
        }
        maven {
            url 'https://maven.aliyun.com/repository/google'
        }
        maven {
            url 'https://maven.aliyun.com/repository/spring'
        }
        maven {
            url 'https://maven.aliyun.com/repository/spring-plugin'
        }
        maven {
            url 'https://maven.aliyun.com/repository/apache-snapshots'
        }
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    // 使用 JUnit 单元测试
    test {
        useJUnitPlatform()
    }
}

subprojects {

    dependencies {
//		implementation project(":iplus-dependencies")

        // webflux -- reactor-core
        implementation group: 'org.springframework.boot', name: 'spring-boot-starter-webflux'

        // aop
        implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'


        // RxJava3
        implementation group: 'io.reactivex.rxjava3', name: 'rxjava', version: rxjava.version

        // junit 前面需要 useJUnitPlatform()
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junit.version
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junit.version

        // swagger
        implementation "io.springfox:springfox-boot-starter:3.0.0"

        // gradle 集成 lombok
        compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.20'
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.20'
        testCompileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.20'
        testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.20'
    }


}

